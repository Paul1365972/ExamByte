plugins {
	id 'java'
	id 'org.springframework.boot' version '3.4.2'
	id 'io.spring.dependency-management' version '1.1.7'
	id 'org.flywaydb.flyway' version '9.22.0'
	id 'checkstyle'
	id 'com.github.spotbugs' version '6.0.27'
	id 'com.diffplug.spotless' version '7.0.2'
	id 'jacoco'
}

group = 'de.paul1365972'
version = '0.0.1-SNAPSHOT'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(21)
	}
}

repositories {
	mavenCentral()
}

dependencies {
	// TODO Clean up unneeded deps
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'
	implementation 'org.thymeleaf.extras:thymeleaf-extras-springsecurity6'
	implementation 'org.springframework.boot:spring-boot-starter-validation'
	implementation 'org.springframework.boot:spring-boot-starter-data-jdbc'
	implementation 'org.springframework.boot:spring-boot-starter-security'
	implementation 'org.springframework.boot:spring-boot-starter-oauth2-client'
	implementation 'org.flywaydb:flyway-core'
	implementation 'org.flywaydb:flyway-database-postgresql:11.3.1'

	developmentOnly 'org.springframework.boot:spring-boot-devtools'

	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'org.springframework.security:spring-security-test'
	testImplementation 'com.tngtech.archunit:archunit-junit5:1.2.1'

	runtimeOnly 'org.postgresql:postgresql'

	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

	compileOnly 'com.github.spotbugs:spotbugs-annotations:4.8.6'

	spotbugsPlugins 'com.h3xstream.findsecbugs:findsecbugs-plugin:1.12.0'
}

tasks.named('test') {
	useJUnitPlatform()
}

spotbugs {
	effort = com.github.spotbugs.snom.Effort.MAX
}

spotbugsMain {
	reports {
		xml.enabled = false
		html.enabled = true
	}
}

spotless {
	java {
		importOrder()
		removeUnusedImports()
		// TODO: Do we want this?
		// cleanthat()
		eclipse()
		formatAnnotations()
	}
	groovyGradle {
		greclipse()
	}
}

jacocoTestReport {
	dependsOn test
	reports {
		xml.required = false
		csv.required = false
		html.required = true
	}
}

test {
	finalizedBy jacocoTestReport
}
